function prompt_jpgrayson_precmd {
  _prompt_jpgrayson_pwd=$(prompt-pwd)
  vcs_info
}

function prompt_jpgrayson_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_jpgrayson_precmd

  zstyle ':prezto:module:prompt' managed 'yes'

  zstyle ':prezto:module:editor:info:completing' format '%B%F{white}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{green}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{3}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%f%b'

  zstyle ':vcs_info:*' unstagedstr '%F{yellow}●%f'
  zstyle ':vcs_info:*' stagedstr '%F{green}●%f'
  zstyle ':vcs_info:*' actionformats "%S" "%F{black}%r/%s/%b %u%c (%a) %m"
  zstyle ':vcs_info:*' formats "%S" "%F{black}%b%f%u%c%m"
  zstyle ':vcs_info:git:*' nopatch-format ' %F{white}%n/%a%f'
  zstyle ':vcs_info:git:*' patch-format ' %F{white}%n/%a%f %F{blue}%p%f'

  _prompt_jpgrayson_pwd=''

  PROMPT='${SSH_TTY:+"%F{red}%n%f%F{cyan}@%m%f "}'
  PROMPT+='%F{blue}${_prompt_jpgrayson_pwd}%f'
  PROMPT+='%(!. %B%F{1}#%f%b.)'
  PROMPT+='${editor_info[keymap]} '

  RPROMPT="%(?:: %F{red}✘ %?%f )"
  RPROMPT+='%30>⋯>${vcs_info_msg_1_}%f'

}

prompt_jpgrayson_setup "$@"
